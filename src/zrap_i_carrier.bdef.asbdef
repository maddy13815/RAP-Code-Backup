managed implementation in class zbp_rap_i_carrier unique;
strict ( 1 );
with draft;

define behavior for ZRAP_I_CARRIER alias Carrier
//persistent table zrap_carrier
draft table ZRAP_D_CARRIER
with unmanaged save
lock master
total etag LastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt
{
  field ( mandatory ) CarrierId, Name, CurrencyCode;
  field ( readonly ) LocalCreatedBy, LocalCreatedAt, LocalLastChangedAt,LocalLastChangedBy, LastChangedAt;
  create;
  update;
  delete;

  association _Connection { create; with draft; }

  draft action Edit;
  draft action Activate;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare;

  mapping for zrap_carrier {
    CarrierId = carrier_id;
    Name = name;
    CurrencyCode = currency_code;
    LocalCreatedBy = local_created_by;
    LocalCreatedAt = local_created_at;
    LocalLastChangedBy = local_last_changed_by;
    LocalLastChangedAt = local_last_changed_at;
    LastChangedAt = last_changed_at;
  }
}

define behavior for zrap_i_connection alias Connection
persistent table zrap_connection
draft table zra_d_connection
lock dependent by _Carrier
authorization dependent by _Carrier
etag dependent by _Carrier
{
  update;
  delete;
  field ( readonly ) CarrierId;
  association _Carrier { with draft; }

  mapping for zrap_connection corresponding {
    CarrierId = carrier_id;
    ConnectionId = connection_id;
    AirportFromId = airport_from_id;
    AirportToId  = airport_to_id;
    DepartureTime = departure_time;
    ArrivalTime = arrival_time;
    Distance = distance;
    DistanceUnit = distance_unit;
  }

}